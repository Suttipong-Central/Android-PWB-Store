buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.28.0'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'realm-android'

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 28
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        flavorDimensions "version"
        multiDexEnabled true
    }
    buildTypes {
        debug {
            applicationIdSuffix ".dev"
            versionNameSuffix "-dev"
            ext.alwaysUpdateBuildId = false
            testCoverageEnabled true
            buildConfigField 'boolean', 'IS_PRODUCTION', 'false'
        }

        staging {
            applicationIdSuffix ".staging"
            versionNameSuffix "-staging"
            signingConfig signingConfigs.config
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable false // Set false when singed new apk
            jniDebuggable false
            renderscriptDebuggable false
            renderscriptOptimLevel 3
            zipAlignEnabled true
            buildConfigField 'boolean', 'IS_PRODUCTION', 'false'
        }

        uat {
            applicationIdSuffix ".uat"
            versionNameSuffix "-uat"
            signingConfig signingConfigs.config
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable false // Set false when singed new apk
            jniDebuggable false
            renderscriptDebuggable false
            renderscriptOptimLevel 3
            zipAlignEnabled true
            buildConfigField 'boolean', 'IS_PRODUCTION', 'false'
        }

        release {
            signingConfig signingConfigs.config
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            renderscriptOptimLevel 3
            zipAlignEnabled true
            buildConfigField 'boolean', 'IS_PRODUCTION', 'true'
        }
    }

    productFlavors {
        pwb {
            applicationId "cenergy.central.com.eordering.pwb"
            manifestPlaceholders = [applicationLabel: "@string/app_name"]
            versionCode 89
            versionName "1.1.2"
        }

        cds {
            applicationId "cenergy.central.com.eordering.cds"
            manifestPlaceholders = [applicationLabel: "@string/app_name"]
            versionCode 89
            versionName "1.1.1"
        }

        rbs {
            applicationId "cenergy.central.com.eordering.rbs"
            manifestPlaceholders = [applicationLabel: "@string/app_name"]
            versionCode 70
            versionName "1.0.10"
        }
    }

    dexOptions {
        maxProcessCount 4
        javaMaxHeapSize "2g"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    androidExtensions {
        experimental = true
    }
}

def supportLibraryVersion = '28.0.0'
def kotlin_version = '1.2.60'

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
    implementation "com.android.support:cardview-v7:$supportLibraryVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibraryVersion"
    implementation "com.android.support:design:$supportLibraryVersion"
    implementation "com.android.support:support-annotations:$supportLibraryVersion"
    implementation "com.android.support:customtabs:$supportLibraryVersion"
    implementation "com.android.support:percent:$supportLibraryVersion"
    implementation 'com.android.support:multidex:1.0.3'

    /* Firebase */
    implementation 'com.google.firebase:firebase-config:17.0.0' // TODO: Update to last version and support androidx

    /* Glide */
    implementation 'com.github.bumptech.glide:glide:3.8.0'
    implementation 'jp.wasabeef:glide-transformations:2.0.1'

    /* Retrofit */
    implementation 'com.squareup.okhttp3:okhttp:3.14.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.12.0'
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'

    /* Gson */
    implementation 'com.google.code.gson:gson:2.8.5'

    /* other */
    implementation 'org.greenrobot:eventbus:3.1.1'
    implementation 'com.scottyab:secure-preferences-lib:0.1.4'
    implementation 'com.mcxiaoke.viewpagerindicator:library:2.4.1'
    implementation 'com.pacioianu.david:ink-page-indicator:1.2.0'
    implementation 'com.google.zxing:core:3.3.0'
    implementation 'com.journeyapps:zxing-android-embedded:3.4.0'
    implementation 'com.rengwuxian.materialedittext:library:2.1.4'
    implementation 'com.github.ksoichiro:android-observablescrollview:1.6.0'
    implementation 'com.jakewharton:butterknife:8.6.0' // TODO: Remove
    implementation 'com.robinhood.ticker:ticker:1.1.1'
    implementation 'joda-time:joda-time:2.9.4'
    implementation 'com.amazonaws:aws-android-sdk-core:2.16.1'
    implementation 'com.amazonaws:aws-android-sdk-s3:2.16.1'
    //implementation 'uk.co.lucasweb:aws-v4-signer-java:1.3'
    kapt 'com.jakewharton:butterknife-compiler:8.6.0' // TODO: Remove
    testImplementation 'junit:junit:4.12'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    //date picker
    implementation 'com.wdullaer:materialdatetimepicker:3.6.4'
    // fabric
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.5@aar') {
        transitive = true
    }
}
apply plugin: 'com.google.gms.google-services'

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == 'com.android.support') {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion "$supportLibraryVersion"
            }
        }
    }
}
repositories {
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
}
