buildscript {
    repositories {
        google()
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.31.2'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'realm-android'

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 29
    buildToolsVersion '29.0.3'
    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 29
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        flavorDimensions "version"
        multiDexEnabled true
        versionCode 152
        versionName "1.2.5"
    }
    buildTypes {
        debug {
            applicationIdSuffix ".dev"
            versionNameSuffix "-dev"
            ext.alwaysUpdateBuildId = false
            testCoverageEnabled true
            buildConfigField 'boolean', 'IS_PRODUCTION', 'false'
        }

        staging {
            applicationIdSuffix ".staging"
            versionNameSuffix "-staging"
            signingConfig signingConfigs.config
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable true // Set false when singed new apk
            jniDebuggable false
            renderscriptDebuggable false
            renderscriptOptimLevel 3
            zipAlignEnabled true
            buildConfigField 'boolean', 'IS_PRODUCTION', 'false'
        }

        uat {
            applicationIdSuffix ".uat"
            versionNameSuffix "-uat"
            signingConfig signingConfigs.config
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable false // Set false when singed new apk
            jniDebuggable false
            renderscriptDebuggable false
            renderscriptOptimLevel 3
            zipAlignEnabled true
            buildConfigField 'boolean', 'IS_PRODUCTION', 'false'
        }

        release {
            signingConfig signingConfigs.config
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            renderscriptOptimLevel 3
            zipAlignEnabled true
            buildConfigField 'boolean', 'IS_PRODUCTION', 'true'
        }
    }

    productFlavors {
        pwb {
            applicationId "cenergy.central.com.eordering.pwb"
            manifestPlaceholders = [applicationLabel: "@string/app_name"]
            buildConfigField 'boolean', 'ENABLE_VERIFY_PRODUCT_SALABEL', 'true'
            buildConfigField 'boolean', 'ENABLE_2H_PICKUP', 'true'
        }

        cds {
            applicationId "cenergy.central.com.eordering.cds"
            manifestPlaceholders = [applicationLabel: "@string/app_name"]
            buildConfigField 'boolean', 'ENABLE_VERIFY_PRODUCT_SALABEL', 'false'
            buildConfigField 'boolean', 'ENABLE_2H_PICKUP', 'false'
        }

        rbs {
            applicationId "cenergy.central.com.eordering.rbs"
            manifestPlaceholders = [applicationLabel: "@string/app_name"]
            buildConfigField 'boolean', 'ENABLE_VERIFY_PRODUCT_SALABEL', 'false'
            buildConfigField 'boolean', 'ENABLE_2H_PICKUP', 'false'
        }
    }

    dexOptions {
        maxProcessCount 4
        javaMaxHeapSize "2g"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    androidExtensions {
        experimental = true
    }
}

ext {
    chuckerVersion = "3.3.0"
}

dependencies {
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'com.google.android.material:material:1.2.0'
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.percentlayout:percentlayout:1.0.0' // TODO: Remove
    implementation 'androidx.multidex:multidex:2.0.1'

    /* Firebase */
    implementation 'com.google.firebase:firebase-config:19.2.0'
    implementation 'com.google.firebase:firebase-analytics:17.4.4'
    implementation 'com.google.firebase:firebase-firestore:21.5.0'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'

    /* Glide */
    implementation 'com.github.bumptech.glide:glide:4.11.0'

    /* Retrofit */
    implementation 'com.squareup.okhttp3:okhttp:3.14.9'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.12.7'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'

    /* chucker */
    implementation "com.github.ChuckerTeam.Chucker:library:$chuckerVersion"
    releaseImplementation "com.github.ChuckerTeam.Chucker:library-no-op:$chuckerVersion"

    /* Gson */
    implementation 'com.google.code.gson:gson:2.8.6'

    /* other */
    implementation 'org.greenrobot:eventbus:3.1.1'
    implementation 'com.scottyab:secure-preferences-lib:0.1.4'
    implementation 'com.mcxiaoke.viewpagerindicator:library:2.4.1'
    implementation 'com.pacioianu.david:ink-page-indicator:1.2.0'
    implementation 'com.google.zxing:core:3.3.0'
    implementation 'com.journeyapps:zxing-android-embedded:3.4.0'
    implementation 'com.rengwuxian.materialedittext:library:2.1.4'
    implementation 'com.robinhood.ticker:ticker:1.1.1'
    implementation 'joda-time:joda-time:2.10.3'
    implementation 'com.amazonaws:aws-android-sdk-core:2.16.1'
    implementation 'com.amazonaws:aws-android-sdk-s3:2.16.1'

    // Date picker
    implementation 'com.wdullaer:materialdatetimepicker:3.6.4'

    // Rating
    implementation 'me.zhanghai.android.materialratingbar:library:1.3.2'

    // Image - Zooming
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'

    testImplementation 'junit:junit:4.12'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}
apply plugin: 'com.google.gms.google-services'
